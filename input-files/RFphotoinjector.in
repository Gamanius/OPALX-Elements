/*
	AWA Gun, configured in this input file for fast execution 
	(low particle number & low fieldsolver resolution, large
	timestep and only one energy bin).
	
	Runs easy on the laptop and is for demonstration 
	puposes only i.e. gives you easy an easy start.

	For medium resolution use the following configuration

	REAL n_particles = 1E5;
	REAL MX = 32;
	REAL MY = 32;
	REAL MZ = 64;
	REAL BINS = 5;
	REAL dTg = 1.0E-13;	

	Andreas Adelmann 20. August 2019
*/

OPTION, PSDUMPFREQ = 10;    // 6d data written every 10th time step (h5).
OPTION, STATDUMPFREQ = 1;   // Beam Stats written every time step (stat).
OPTION, BOUNDPDESTROYFQ=10; // Delete lost particles, if any out of 10 \sigma
OPTION, AUTOPHASE=4;        // Autophase is on, and phase of max energy
                            // gain will be found automatically for cavities.
OPTION, VERSION=10900;

Title, string="AWA Photoinjector";

Value,{OPALVERSION};

//----------------------------------------------------------------------------
//Global Parameters

REAL rf_freq             = 1.3e9;    // RF frequency. (Hz)
REAL n_particles         = 1E4;      // Number of particles in simulation.
REAL beam_bunch_charge   = 1e-9;     // Charge of bunch. (C)

//----------------------------------------------------------------------------
//Fieldsolver configuration
REAL MX = 8;                                                                                               
REAL MY = 8;                                                                                               
REAL MZ = 8;
REAL BINS = 1;

//----------------------------------------------------------------------------
// Gun timestep

REAL dTG = 1.0E-13;

//Initial Momentum Calculation
REAL Edes    = 1.4e-9; //initial energy in GeV
REAL gamma   = (Edes+EMASS)/EMASS; 
REAL beta    = sqrt(1-(1/gamma^2));
REAL P0      = gamma*beta*EMASS;    //inital z momentum

//Printing initial energy and momentum to terminal output.
value , {Edes, P0};

//----------------------------------------------------------------------------
// RF photoinjector (Gun)
//

REAL gun_inj_phase = 0.0;  // Rf injection phase (in degrees) with restpect 
                           // to max phase. i.e. 0.0 inj phase corresponds 
                           // to running on crest (max energy).


GUN1:   RFCavity, L = 0.2927, VOLT = 60.0, ELEMEDGE=0.0, TYPE = "STANDING",
        FMAPFN = "fieldmaps/DriveGun.T7", FREQ = 1300.0, LAG = (gun_inj_phase*Pi)/180.0; 


GUN2:   RFCavity, L = 0.2927, VOLT = 60.0, ELEMEDGE=0.3, TYPE = "STANDING",
        FMAPFN = "fieldmaps/DriveGun.T7", FREQ = 1300.0, LAG = (gun_inj_phase*Pi)/180.0; 


REAL KSBF = 0.162544398/1.3528;

BF: Solenoid, L = 0.5, ELEMEDGE=0.0, KS = KSBF, FMAPFN = "fieldmaps/BF_550.T7";


REAL I  = 273; //[A]
REAL SF = (I/440)*1.973966/3.2306;

// M:  Solenoid, L = 0.5, ELEMEDGE=0.0, KS = SF, FMAPFN = "M_440.T7";

value,{KSBF,SF};

//----------------------------------------------------------------------------
//Drift after gun.

DR1: DRIFT, L = 0.4, ELEMEDGE = 0.2;

//----------------------------------------------------------------------------
// DEFINE BEAM LINE

GS:  Line = (BF, GUN1,GUN2);

Dist: DISTRIBUTION, TYPE= GAUSS, SIGMAX=1e-3, SIGMAY=1e-3;

Fs_SC: FIELDSOLVER, TYPE=NONE, MX=64; 


//----------------------------------------------------------------------------
// Electron Beam Definition

BEAM1:  BEAM, PARTICLE = ELECTRON, pc = P0, NPART = n_particles,
        BFREQ = rf_freq, BCURRENT = beam_bunch_charge * rf_freq * 1E6, CHARGE = -1;

TRACK, LINE = GS, BEAM = BEAM1, MAXSTEPS = 2, DT = {dTg}, ZSTOP={1.0};
RUN, METHOD = "PARALLEL", BEAM = BEAM1, 
    FIELDSOLVER = FS_SC, DISTRIBUTION = Dist;
ENDTRACK;
