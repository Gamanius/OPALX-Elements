Title, string="Cyclotron_Multipacting_Simulation_example";

Option, ECHO=FALSE;
Option, INFO=FALSE;

Option, PSDUMPFREQ=1;
Option, STATDUMPFREQ=1;
Option, PPDEBUG=FALSE;
Option, SURFDUMPFREQ=100;

// Set an upper limit of simulation particle number
// to prevent memory overflow.

MAXPARTSNUM=1000000;

// SECONDARYFLAG = 1: Using Furman-Pivi's model
// SURFMATERIAL=0: surface material is copper
// Set NEMISSIONMODE=false will use re-normalize
// simulation particle approach.
// Set the field enhancement factor FNBETA to
// a very small number to prevent field
// emission.

DistSurf: DISTRIBUTION, DISTRIBUTION = "SURFACEEMISSION",
                        NPDARKCUR =0,INWARDMARGIN = 0.0,
                        FNBETA = 0.1, FNMAXEMI = 2,
                        SECONDARYFLAG = 1,
                        NEMISSIONMODE=false,
                        SURFMATERIAL=0;

// INWARDMARGIN: seed electron positions along the
// inward normal w.r.t the boundary surface.
DistSurf1: DISTRIBUTION,
           DISTRIBUTION = "SURFACERANDCREATE",
           INWARDMARGIN = 0.0, NPDARKCUR =10000,
           EINITHR = 0.2;

// For multipacting study of cyclotron cavity,
// the axis z in geometry file is actually axis y
// in ParallelTTracker, so we need to shift z
// coordinates of the geometry by specifying ZSHIFT to make
// sure that the z coordinates read in by ParallelTTracker
// will be correct.

ge: GEOMETRY, FGEOM="../10.h5", S=0.0,
       ZSHIFT=0.631, DISTRS={DistSurf, DistSurf1};

Box: RFCavity, PLENGTH = 1.262, VOLT = 1,
               GEOMETRY = ge, FMAPFN = "../CyciaeEM.h5",
	       ELEMEDGE =0, FAST=true,
               FREQ =44.6, LAG = 0.0,
               DX = 0, DY = 0;

// This element is used to model the magnetic field in
// the valley of a cyclotron, where the RF cavity is installed.

Mag: CYCLOTRONVALLEY, FMAPFN = "../CyciaeMagReal.h5",
                      ELEMEDGE =0, DX = 0, DY = 0;

Benchmark: Line = (Box, Mag);

Fs1:FIELDSOLVER, FSTYPE = NONE, MX = 32,
                 MY = 32, MT = 256,
		 PARFFTX = true, PARFFTY = true,
                 PARFFTT = false, BCFFTX = open,
                 BCFFTY = open, BCFFTT = open,
		 BBOXINCR = 0.1, GREENSF = INTEGRATED;

qb=0.2e-9;
bfreq=300;
bcurrent=qb*bfreq;

beam1: BEAM, PARTICLE = ELECTRON, pc = P0,
             NPART = 2000, BFREQ = bfreq ,
             BCURRENT = bcurrent, CHARGE =-1;

Select, Line=Benchmark;

track, line= Benchmark, beam=beam1,
       MAXSTEPS=23000, DT=4e-12, ZSTOP=3;

run, method = "PARALLEL-T", beam = beam1,
     fieldsolver = Fs1, MULTIPACTING=true;
endtrack;

Quit;